plugins {
    id "com.github.johnrengelman.shadow" version "4.0.3"
    id 'org.beryx.jlink' version '2.2.1'
    id 'application'
    id 'maven-publish'
    id 'idea'
}

group 'org.mrcube'
version = '1.0.4'
mainClassName = 'org.mrcube.MR3'
sourceCompatibility = targetCompatibility = '11'

project.ext {
    moduleName = 'org.mrcube.mrcube'
    artifactId = 'mrcube'
    defaultEncoding = 'UTF-8'
}

repositories {
    jcenter()
    maven {
        url 'http://jfontchooser.sourceforge.jp/repository/'
    }
}

wrapper {
    gradleVersion = '5.0'
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

idea {
    module {
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

test {
    useJUnitPlatform()
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'org.mrcube.MR3'
    }
    archiveName = "mrcube.jar"
}

jlink {
    mergedModule {
    }
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'mrcube'
    }
}

dependencies {
    compile 'jgraph:jgraph:5.13.0.0'
    compile 'org.apache.jena:jena-core:3.9.0'
    compile 'say.swing:jfontchooser:1.0.5'
//    compile 'org.slf4j:slf4j-log4j12:1.7.25'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

task zipApp(type: Zip, dependsOn: startShadowScripts) {
    archiveName = "mrcube-${version}.${extension}"

    from('./README.md')
    from('./README.ja.md')
    from('./src/main/resources/gpl.md')
    into('bin') {
        from('build/scriptsShadow/mrcube')
        from('build/scriptsShadow/mrcube.bat')
    }

    into('lib') {
        from('build/libs/mrcube.jar')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.mrcube'
            artifactId = 'mrcube'
            version = '1.0.4'

            from components.java
        }
    }
}

defaultTasks 'zipApp'